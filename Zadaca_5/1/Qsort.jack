class Qsort
{
	function void swap(Array A, int i, int j) {
		var int temp;
		let temp = A[i];
		let A[i] = A[j];
		let A[j] = temp;
		return;
	}
	
	function int partition(Array A, int l, int r) {
        var int pivot, i, j;
        let pivot = A[l];
        let i = l;
        let j = l + 1;
        while (~(j > r)) {
            if (~(A[j] > pivot)) {
                let i = i + 1;
                do Qsort.swap(A, i, j);
            }
            let j = j + 1;
        }
        do Qsort.swap(A, i, l);
        return i;
    }
	
	function void qsort(Array A, int l, int r) {
        var int m;
        if (l < r) {
            let m = Qsort.partition(A, l, r);
            do Qsort.qsort(A, l, m - 1);
            do Qsort.qsort(A, m + 1, r);
        }
        return;
    }
}